apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/sandbox/java'
        }
        resources {
            srcDir 'src/sandbox/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    compile "org.slf4j:slf4j-api:1.7.24"
    compile "ru.opensecreto:openutils:1.0"
    compile 'org.bitlet:weupnp:0.1.4'

    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile 'org.testng:testng:6.9.12'
    testCompile "org.slf4j:slf4j-jdk14:1.7.24"
}

jacocoTestReport.dependsOn.add(test)
task coverage {}
coverage.dependsOn.add(jacocoTestReport)

test.useTestNG()
task prepareTestWorkingDir {
    File outputDir = new File("$buildDir/test-output")
    if (!outputDir.exists()) {
        outputDir.mkdirs();
    } else {
        GFileUtils.cleanDirectory(outputDir)
    }
    test.workingDir = outputDir
}
test.dependsOn.add(prepareTestWorkingDir)

tasks.withType(Test) {
    int time = 0;
    afterTest { desc, result ->
        time += result.getEndTime() - result.getStartTime()
    }

    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)" +
                        " Time: " + time / 1000 + "secs "
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
